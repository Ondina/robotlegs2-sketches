<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 width="222"
		 height="361"
		 horizontalCenter="0"
		 verticalCenter="0"
		 addedToStage="onAddedToStage(event);"
		 creationComplete="onCreationComplete(event)"
		 removedFromStage="onRemovedFromStage(event);">
	<fx:Declarations>
		<mx:Validator id="userNameValidator"
					  required="true"
					  source="{userName}"
					  property="text"/>
		<mx:Validator id="userPasswordValidator"
					  required="true"
					  source="{userPassword}"
					  property="text"/>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;

			import yourdomain.modules.popupStuff.controllers.events.SomeEvent;
			import yourdomain.modules.popupStuff.models.SomeVO;
			
			/**
			 *
			 * @author Ondina D. F.
			 */
			protected var someVO:SomeVO;
			protected var validators:Array;

			protected function onCreationComplete(event:FlexEvent):void
			{
				validators=[userNameValidator, userPasswordValidator];
				removeEventListener(FlexEvent.CREATION_COMPLETE, onCreationComplete);
				addListeners();
				userName.setFocus();
			}

			protected function addListeners():void
			{
				someButton.addEventListener(MouseEvent.CLICK, validateForm);
				userPassword.addEventListener(MouseEvent.DOUBLE_CLICK, onEmptyFields);
				userName.addEventListener(MouseEvent.DOUBLE_CLICK, onEmptyFields);
			}

			protected function onAddedToStage(event:Event):void
			{
				addEventListener(KeyboardEvent.KEY_DOWN, keyDownHandler);
			}

			override protected function keyDownHandler(event:KeyboardEvent):void
			{
				super.keyDownHandler(event);
				if (event.keyCode == Keyboard.ENTER)
					validateForm();
			}

			protected function resetInputs():void
			{
				userName.text="";
				userPassword.text="";
			}

			protected function validateForm(event:MouseEvent=null):void
			{
				if (Validator.validateAll(validators).length > 0)
					return;
				onDataPing();
			}

			protected function onDataPing():void
			{
				someVO=new SomeVO();
				someVO.user_name=userName.text;
				someVO.user_password=userPassword.text;
				dispatchEvent(new SomeEvent(SomeEvent.DATA_PING, someVO));
			}

			protected function onRemovedFromStage(event:Event):void
			{
				removeListeners();
				resetInputs();
			}

			protected function removeListeners():void
			{
				removeEventListener(KeyboardEvent.KEY_DOWN, keyDownHandler);
				removeEventListener(Event.ADDED_TO_STAGE, onAddedToStage);
				removeEventListener(Event.REMOVED_FROM_STAGE, onRemovedFromStage);
				someButton.removeEventListener(MouseEvent.CLICK, validateForm);
				userPassword.removeEventListener(MouseEvent.DOUBLE_CLICK, onEmptyFields);
				userName.removeEventListener(MouseEvent.DOUBLE_CLICK, onEmptyFields);
			}

			protected function onEmptyFields(event:MouseEvent):void
			{
				event.currentTarget.text="";
			}

			public function onDataPong(someVO:SomeVO):void
			{
				receivedMessage.text="Master says: " + someVO.someMessage;
			}
		]]>
	</fx:Script>
	<s:layout>
		<s:BasicLayout/>
	</s:layout>
	<s:Panel x="10"
			 y="10"
			 title="Some View"
			 width="200"
			 height="175">
		<mx:Label x="10"
				  y="28"
				  text="username"/>
		<s:TextInput x="92"
					 y="27.5"
					 id="userName"
					 text="rl"
					 width="90"
					 doubleClickEnabled="true"/>
		<mx:Label x="10"
				  y="65"
				  text="password"/>
		<s:TextInput x="92"
					 y="64.5"
					 id="userPassword"
					 text="rl"
					 displayAsPassword="true"
					 doubleClickEnabled="true"
					 width="90"/>
		<s:Button y="106"
				  label="send"
				  id="someButton"
				  x="123"/>
	</s:Panel>
	<s:Label x="12"
			 y="231"
			 text="From Popups Master:"/>
	<s:TextArea id="receivedMessage"
				x="10"
				y="250"
				width="200"
				height="95"/>
</s:Group>
